import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'org.detoeuf.swagger-codegen' version '1.6.3'
    id 'java'
}

task generateSources {
    swagger {
        inputSpec = project.file('swagger.json').absolutePath

        language = 'jaxrs'

        additionalProperties = [
                'invokerPackage'   : 'io.swagger.trade.server.jersey',
                'modelPackage'     : 'io.swagger.trade.server.jersey.model',
                'apiPackage'       : 'io.swagger.trade.server.jersey.api',
                'library'          : 'jersey2',
                'serializableModel': 'true',
                'dateLibrary'      : 'java7'
        ]
    }
}

// Load the specified port number from the config.properties file
Properties properties = new Properties()
properties.load(project.parent.file('/config/config.properties').newDataInputStream())
def httpPort = properties.getProperty('server.port.http')

// Copy the swagger.json file to the static swaggerui resources to make it accessible
// Replace the $PORT_NUMBER$ placeholder with the actual HTTP port from the properties file
task copySwaggerFile(type: Copy) {
    from project.file('swagger.json')
    into 'src/main/resources/swaggerui'
    filter(ReplaceTokens, tokens: [port_number:httpPort])
}

configurations {
    compile.extendsFrom apiCompile
    runtime.extendsFrom apiRuntime
}

// Create a new source set for the generated API classes
sourceSets {
    api
}
// Disable compiling api source set, since the used classes are compiled through 'compileJava' at once
compileApiJava.enabled = false
// Make these classes visible to the ones copied manually to the main source set
sourceSets.api.java.srcDirs += "${project.buildDir.path}/generated-sources/swagger/src/gen/java"
sourceSets.api.java.srcDirs += "${project.buildDir.path}/generated-sources/swagger/src/main/java"

// Add the non-adapted, generated API source files to the list of sources to compile
compileJava {
    dependsOn generateSources, copySwaggerFile
    source += fileTree(dir: "${project.buildDir.path}/generated-sources/swagger/src/",
            includes: ['gen/java/io/swagger/trade/server/jersey/model/**',
                       'main/java/io/swagger/trade/server/jersey/api/factories/**'])
}

dependencies {
    // Add a dependency from the main source set to the api source set
    compile sourceSets.api.output

    compile project (':utils'), project(':dataManagement')

    compile group: 'org.eclipse.jetty', name: 'jetty-server', version: '9.3.14.v20161028'
    compile group: 'org.eclipse.jetty', name: 'jetty-servlet', version: '9.3.14.v20161028'

//    compile group: 'org.eclipse.jetty', name: 'jetty-alpn-server', version: '9.3.14.v20161028'
//    compile group: 'org.eclipse.jetty.http2', name: 'http2-server', version: '9.3.14.v20161028'
//    runtime group: 'org.mortbay.jetty.alpn', name: 'alpn-boot', version: '8.1.10.v20161026'

    compile group: 'org.glassfish.jersey.core', name: 'jersey-server', version: '2.24'
    compile group: 'org.glassfish.jersey.containers', name: 'jersey-container-servlet-core', version: '2.24'
    compile group: 'org.glassfish.jersey.containers', name: 'jersey-container-jetty-http', version: '2.24'
    compile group: 'org.glassfish.jersey.media', name: 'jersey-media-moxy', version: '2.24'

    compile group: 'org.glassfish.jersey.media', name: 'jersey-media-json-jackson', version: '2.24'
    apiCompile group: 'org.glassfish.jersey.media', name: 'jersey-media-multipart', version: '2.24'
    compile group: 'com.fasterxml.jackson.jaxrs', name: 'jackson-jaxrs-json-provider', version: '2.8.5'
    apiCompile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.8.5'
    apiCompile group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.8.5'
    apiCompile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.8.5'
    apiCompile group: 'com.sun.jersey', name: 'jersey-core', version: '1.19.3'

    // Add swagger dependencies
    apiCompile group: 'io.swagger', name: 'swagger-annotations', version: '1.5.8'
    compile group: 'io.swagger', name: 'swagger-jersey2-jaxrs', version: '1.5.10'
}