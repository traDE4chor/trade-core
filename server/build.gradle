import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'org.detoeuf.swagger-codegen' version '1.6.3'
    id 'java'
}

task generateSources {
    swagger {
        inputSpec = project.file('swagger.json').absolutePath

        language = 'jaxrs'

        additionalProperties = [
                'invokerPackage'   : 'io.swagger.trade.server.jersey',
                'modelPackage'     : 'io.swagger.trade.server.jersey.model',
                'apiPackage'       : 'io.swagger.trade.server.jersey.api',
                'library'          : 'jersey2',
                'jackson'          : 'true',
                'serializableModel': 'true',
                'dateLibrary'      : 'java7'
        ]
    }
}

// Load the specified port number from the config.properties file
Properties properties = new Properties()
properties.load(project.parent.file('/config/config.properties').newDataInputStream())
def httpPort = properties.getProperty('server.port.http')

// Copy the swagger.json file to the static swaggerui resources to make it accessible
// Replace the $PORT_NUMBER$ placeholder with the actual HTTP port from the properties file
task copySwaggerFile(type: Copy) {
    from project.file('swagger.json')
    into 'src/main/resources/swaggerui'
    filter(ReplaceTokens, tokens: [port_number:httpPort])
}

configurations {
    api
}

// Create a new source set for the generated API classes
sourceSets {
    api
}
// Disable compiling api source set, since the used classes are compiled through 'compileJava' at once
compileApiJava.enabled = false
// Make these classes visible to the ones copied manually to the main source set
sourceSets.api.java.srcDirs += "${project.buildDir.path}/generated-sources/swagger/src/gen/java"
sourceSets.api.java.srcDirs += "${project.buildDir.path}/generated-sources/swagger/src/main/java"

// Add the non-adapted, generated API source files to the list of sources to compile
compileJava {
    dependsOn generateSources, copySwaggerFile
    source += fileTree(dir: "${project.buildDir.path}/generated-sources/swagger/src/",
            includes: ['gen/java/io/swagger/trade/server/jersey/model/**',
                       'main/java/io/swagger/trade/server/jersey/api/factories/**'])
}

ext {
    swagger_version = "1.5.8"
    jackson_version = "2.7.5"
    jersey_version = "2.22.2"
}

dependencies {
    // Add a dependency from the main source set to the api source set
    compile sourceSets.api.output

    // Add all api dependencies to the main source set
    compile configurations.api

    // Add all api dependencies also to the api source set
    apiCompile configurations.api

    compile project (':utils'), project(':dataManagement')

    compile group: 'org.eclipse.jetty', name: 'jetty-server', version: '9.3.14.v20161028'
    compile group: 'org.eclipse.jetty', name: 'jetty-servlet', version: '9.3.14.v20161028'

//    compile group: 'org.eclipse.jetty', name: 'jetty-alpn-server', version: '9.3.14.v20161028'
//    compile group: 'org.eclipse.jetty.http2', name: 'http2-server', version: '9.3.14.v20161028'
//    runtime group: 'org.mortbay.jetty.alpn', name: 'alpn-boot', version: '8.1.10.v20161026'

    compile group: 'org.glassfish.jersey.core', name: 'jersey-server', version: "$jersey_version"
    compile group: 'org.glassfish.jersey.containers', name: 'jersey-container-servlet-core', version: "$jersey_version"
    compile group: 'org.glassfish.jersey.containers', name: 'jersey-container-jetty-http', version: "$jersey_version"
    compile group: 'org.glassfish.jersey.media', name: 'jersey-media-moxy', version: "$jersey_version"
    compile group: 'org.glassfish.jersey.media', name: 'jersey-media-json-jackson', version: "$jersey_version"
    compile group: 'com.fasterxml.jackson.jaxrs', name: 'jackson-jaxrs-json-provider', version: "$jackson_version"

    compile "com.fasterxml.jackson.datatype:jackson-datatype-joda:$jackson_version"
    compile "com.brsanthu:migbase64:2.2"

    // Add dependencies required by API and MAIN Java classes
    api group: 'javax.servlet', name: 'javax.servlet-api', version: '3.1.0'
    api group: 'io.swagger', name: 'swagger-jersey2-jaxrs', version: "$swagger_version"
    api "io.swagger:swagger-annotations:$swagger_version"
    api "org.glassfish.jersey.media:jersey-media-multipart:$jersey_version"
    api group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: "$jackson_version"
    api group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: "$jackson_version"
    api group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: "$jackson_version"
}