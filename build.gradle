group 'de.unistuttgart.iaas.trade'
version '1.0-SNAPSHOT'

apply plugin: 'application'

sourceCompatibility = 1.8

mainClassName = 'org.trade.core.TraDENode'
applicationName = 'traDE'

repositories {
    jcenter()
    mavenCentral()
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integrationTest/java')
        }
        resources.srcDir file('src/integrationTest/resources')
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
    integrationTestCompile group: 'commons-io', name: 'commons-io', version: '2.5'
    integrationTestCompile project(path: ':utils', configuration: 'testOutput')

    compile project(':models'), project(':persistence'), project(':utils'), project(':server'), project(':client')
}

// Summarized configurations, dependencies which are required in common by all sub projects
subprojects {
    apply plugin: 'java'

    repositories {
        jcenter()
        mavenCentral()
    }

    configurations.all {
        exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    }

    dependencies {
        compile group: 'de.slub-dresden', name: 'urnlib', version: '1.1.0'

        // Logging dependencies using slf4j API and Logback
        compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.21'
        compile group: 'ch.qos.logback', name: 'logback-core', version: '1.1.7'
        compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.1.7'

        testCompile group: 'junit', name: 'junit', version: '4.11'
    }

    jar.baseName = 'traDE-'+project.name

    group 'de.unistuttgart.iaas.trade'
    version '1.0-SNAPSHOT'
    sourceCompatibility = 1.8
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.3'
}

task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
    // By default gradle skips tasks whose inputs and outputs are up to date.
    // Uncomment the following line to enforce the execution of the integration test whenever the corresponding task
    // is triggered no matter if something has changed or not.
    // outputs.upToDateWhen { false }
}

// Include the integration tests into the build process. Uncomment the following two lines if you prefer to trigger
// the integration tests only manually and not for each build.
// check.dependsOn integrationTest
// integrationTest.mustRunAfter test

// Ensure that distinct HTML reports for different types of tests are created
tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
}

startScripts {
    classpath += files('config')
    doLast {
        def windowsScriptFile = file getWindowsScript()
        def unixScriptFile = file getUnixScript()
        windowsScriptFile.text = windowsScriptFile.text.replace('%APP_HOME%\\lib\\config', '%APP_HOME%\\config')
        unixScriptFile.text = unixScriptFile.text.replace('$APP_HOME/lib/config', '$APP_HOME/config')
    }
}

distributions {
    main {
        baseName = 'traDE-all'
        contents {
            from('config') {
                into 'config'
            }
            from('ssl') {
                into 'ssl'
            }
        }
    }
}

task createKeystoreFile {
    def keystoreFolder = file('ssl')
    if (file(keystoreFolder).exists()) {
        keystoreFolder.deleteDir()
    }
    keystoreFolder.mkdir()
    ant.genkey(alias:'traDE', keystore:'ssl/keystore.jks', storepass:'someKeyStorePassword', keyalg:'RSA',
            dname:'CN=trade4chor.github.io, O=TraDE, C=DE', keysize:2048)
}
assembleDist.dependsOn createKeystoreFile

jar {
    baseName = 'traDE-core'
}

task runAppInIDE(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath + files('config')
    main = 'org.trade.core.TraDENode'
}