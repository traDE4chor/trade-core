buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath('io.swagger:swagger-codegen:2.3.0')
    }
}

plugins {
    id 'java'
}

import io.swagger.codegen.config.CodegenConfigurator
import io.swagger.codegen.DefaultGenerator

def swaggerInput = file('src/main/resources/swagger.yaml').absolutePath
def swaggerOutputDir = file('build/generated-sources/swagger')
task generateClient {
    inputs.file(swaggerInput)
    outputs.dir(swaggerOutputDir)
    doLast {
        def config = new CodegenConfigurator()
        config.setInputSpec(swaggerInput)
        config.setOutputDir(swaggerOutputDir.path)
        config.setLang('java')
        config.setAdditionalProperties([
                'invokerPackage': 'io.swagger.trade.client.jersey',
                'modelPackage'  : 'io.swagger.trade.client.jersey.model',
                'apiPackage'    : 'io.swagger.trade.client.jersey.api',
                'dateLibrary'   : 'java8',
                'library'          : 'jersey2',
                'jackson'          : 'true',
                'serializableModel': 'true'
        ])
        new DefaultGenerator().opts(config.toClientOptInput()).generate()
    }
}

// Copy the swagger.json file to the resources to make it accessible
// Replace the $PORT_NUMBER$ placeholder with the actual HTTP port from the properties file
task copySwaggerFile(type: Copy) {
    from project(':server').file('swagger.yaml')
    into 'src/main/resources'
}

// Add generated classes to the main source set
sourceSets.main.java.srcDirs += "${project.buildDir.path}/generated-sources/swagger/src/main/java"
sourceSets.main.resources.srcDirs += "${project.buildDir.path}/generated-sources/swagger/docs"

// Add dependencies to
compileJava {
    dependsOn generateClient, copySwaggerFile
}

ext {
    swagger_annotations_version = "1.5.15"
    jackson_version = "2.8.9"
    jersey_version = "2.22.2"
}

dependencies {
    compile project (':server')
    
    compile "io.swagger:swagger-annotations:$swagger_annotations_version"
    compile "org.glassfish.jersey.core:jersey-client:$jersey_version"
    compile "org.glassfish.jersey.media:jersey-media-multipart:$jersey_version"
    compile "org.glassfish.jersey.media:jersey-media-json-jackson:$jersey_version"
    compile "com.fasterxml.jackson.core:jackson-core:$jackson_version"
    compile "com.fasterxml.jackson.core:jackson-annotations:$jackson_version"
    compile "com.fasterxml.jackson.core:jackson-databind:$jackson_version"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jackson_version"
    compile "com.brsanthu:migbase64:2.2"
}